# Maintainer: mrdotx <klassiker@gmx.de>
_pkgname=inkscape
pkgname=inkscape-stripped
pkgver=1.3.1
pkgrel=1
pkgdesc='Professional vector graphics editor (stripped from most unnecessary dependencies and extensions)'
url='https://inkscape.org'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=(
    'cmake'
    'boost'
    'git'
)
depends=(
    'boost-libs'
    'double-conversion'
    'gc'
    'graphicsmagick'
    'gsl'
    'gspell'
    'gtkmm3'
    'gtksourceview4'
    'lcms2'
    'lib2geom'
    'libcdr'
    'libjpeg-turbo'
    'libsoup'
    'libvisio'
    'libxslt'
    'poppler-glib'
    'potrace'
    'python'
    # 'python-appdirs'
    # 'python-beautifulsoup4'
    # 'python-cachecontrol'
    # 'python-cairo'
    # 'python-certifi'
    # 'python-chardet'
    # 'python-coverage'
    # 'python-cssselect'
    # 'python-filelock'
    # 'python-gobject'
    # 'python-idna'
    # 'python-lockfile'
    'python-lxml'
    # 'python-msgpack'
    'python-numpy'
    # 'python-packaging'
    # 'python-pillow'
    # 'python-pyserial'
    # 'python-requests'
    # 'python-six'
    # 'python-urllib3'
    # 'python-zstandard'
    # 'scour'
    'ttf-font'
)
optdepends=(
    'fig2dev: xfig input'
    'gvfs: import clip art'
    'pstoedit: latex formulas'
    # this pulls in texlive-basic, so should suffice
    'texlive-pstricks: latex formulas'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
_archivever=${pkgver//./_}
_archive="inkscape-INKSCAPE_$_archivever"
source=(
    "https://gitlab.com/inkscape/inkscape/-/archive/INKSCAPE_$_archivever/$_archive.tar.bz2"
    'inkscape-lib2geom::git+https://gitlab.com/inkscape/lib2geom.git'
    'inkscape-libcroco::git+https://gitlab.com/inkscape/libcroco.git'
    # 'inkscape-themes::git+https://gitlab.com/inkscape/themes.git'
    'inkscape-extensions::git+https://gitlab.com/inkscape/extensions.git'
    # 'inkscape-extras-inkscape-import-clipart::git+https://gitlab.com/inkscape/extras/inkscape-import-clipart.git'
    # 'inkscape-extras-extension-xaml::git+https://gitlab.com/inkscape/extras/extension-xaml.git'
    # 'inkscape-extras-extensions-gcodetools::git+https://gitlab.com/inkscape/extras/extensions-gcodetools.git'
    # 'inkscape-extras-extension-manager::git+https://gitlab.com/inkscape/extras/extension-manager.git'
)
b2sums=('f59b3907069590df7d1157ef5e0e957eba0ad0134d2c9a3a0577b01ab9dc2cf301fc41b261ac6296ae9dd2f5354f70a5274e254e8c5b753783ff81f89cb8388d'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
    cd "$_archive"

    ## 3rdparty
    _extra="$srcdir/$_archive/src/3rdparty/2geom"
    rm -rf "$_extra"
    ln -s "$srcdir/inkscape-lib2geom" "$_extra"
    # mkdir -p "$_extra"
    _extra="$srcdir/$_archive/src/3rdparty/libcroco"
    rm -rf "$_extra"
    ln -s "$srcdir/inkscape-libcroco" "$_extra"
    # mkdir -p "$_extra"

    ## themes
    _extra="$srcdir/$_archive/share/themes"
    rm -rf "$_extra"
    # ln -s "$srcdir/inkscape-themes" ""$_extra"
    mkdir -p "$_extra"

    ## extensions
    _extra="$srcdir/$_archive/share/extensions"
    rm -rf "$_extra"
    ln -s "$srcdir/inkscape-extensions" "$_extra"
    # mkdir -p "$_extra"

    ## extras
    _extra="$srcdir/$_archive/share/extensions/other/clipart"
    rm -rf "$_extra"
    # ln -s "$srcdir/inkscape-extras-inkscape-import-clipart" "$_extra"
    mkdir -p "$_extra"
    _extra="$srcdir/$_archive/share/extensions/other/extension-xaml"
    rm -rf "$_extra"
    # ln -s "$srcdir/inkscape-extras-extension-xaml" "$_extra"
    mkdir -p "$_extra"
    _extra="$srcdir/$_archive/share/extensions/other/gcodetools"
    rm -rf "$_extra"
    # ln -s "$srcdir/inkscape-extras-extensions-gcodetools" "$_extra"
    mkdir -p "$_extra"
    _extra="$srcdir/$_archive/share/extensions/other/inkman"
    rm -rf "$_extra"
    # ln -s "$srcdir/inkscape-extras-extension-manager" "$_extra"
    mkdir -p "$_extra"
}

build() {
    mkdir -p build
    export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
    cmake -H$_archive -Bbuild \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DWITH_DBUS=ON \

    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --build build --target install
}
