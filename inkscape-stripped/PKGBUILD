# Maintainer: mrdotx <klassiker@gmx.de>
_pkgname=inkscape
pkgname=inkscape-stripped
pkgdesc='Professional vector graphics editor (stripped from most unnecessary dependencies and extensions)'
pkgver=1.3.2
pkgrel=5
url='https://inkscape.org'
arch=('x86_64')
license=('GPL-3.0-or-later')
makedepends=(
    'cmake'
    'boost'
    'git'
    'glib2-devel'
)
depends=(
    'atkmm'
    'boost-libs'
    'cairo'
    'cairomm'
    'double-conversion'
    'fontconfig'
    'freetype2'
    'gc'
    'gcc-libs'
    'gdk-pixbuf2'
    'glib2'
    'glibc'
    'glibmm'
    # 'gobject-introspection-runtime'
    'graphicsmagick'
    'gsl'
    'gspell'
    'gtk3'
    'gtkmm3'
    'gtksourceview4'
    'harfbuzz'
    'hicolor-icon-theme'
    'lcms2'
    'lib2geom'
    'libcdr'
    'libepoxy'
    'libjpeg-turbo'
    'libpng'
    'librevenge'
    'libsigc++'
    'libsoup'
    'libvisio'
    'libwpg'
    'libx11'
    'libxml2'
    'libxslt'
    # 'pango'
    # 'pangomm'
    # 'poppler'
    'poppler-glib'
    'potrace'
    'python'
    # 'python-appdirs'
    # 'python-beautifulsoup4'
    # 'python-cachecontrol'
    # 'python-cairo'
    # 'python-certifi'
    # 'python-chardet'
    # 'python-coverage'
    # 'python-cssselect'
    # 'python-filelock'
    # 'python-gobject'
    # 'python-idna'
    # 'python-lockfile'
    'python-lxml'
    # 'python-msgpack'
    'python-numpy'
    # 'python-packaging'
    # 'python-pillow'
    # 'python-pyserial'
    # 'python-requests'
    # 'python-six'
    # 'python-urllib3'
    # 'python-zstandard'
    'readline'
    # 'scour'
    'ttf-font'
    'zlib'
)
optdepends=(
    'fig2dev: xfig input'
    'gvfs: import clip art'
    'pstoedit: latex formulas'
    # this pulls in texlive-basic, so should suffice
    'texlive-pstricks: latex formulas'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
    "git+https://gitlab.com/inkscape/inkscape.git#tag=INKSCAPE_${pkgver//./_}"
    'inkscape-lib2geom::git+https://gitlab.com/inkscape/lib2geom.git'
    # 'inkscape-libcroco::git+https://gitlab.com/inkscape/libcroco.git'
    # 'inkscape-themes::git+https://gitlab.com/inkscape/themes.git'
    'inkscape-extensions::git+https://gitlab.com/inkscape/extensions.git'
    # 'inkscape-extras-inkscape-import-clipart::git+https://gitlab.com/inkscape/extras/inkscape-import-clipart.git'
    # 'inkscape-extras-extension-xaml::git+https://gitlab.com/inkscape/extras/extension-xaml.git'
    # 'inkscape-extras-extensions-gcodetools::git+https://gitlab.com/inkscape/extras/extensions-gcodetools.git'
    # 'inkscape-extras-extension-manager::git+https://gitlab.com/inkscape/extras/extension-manager.git'
    '0001-Remove-device-manager.h-.cpp-and-src-ui-dialog-input.patch'
)
b2sums=('fa950706618c913149bc368c14c6d9a3fba9a30544c6afaf92f9cc842093a72a7df91d20754bc2879da29add7b5df85da09995aee2f54e83d8bf044cbd2cdfd3'
        'SKIP'
        'SKIP'
        'cf657acdea913ea183842d742af9c34247103bf78928ffba772e3c5ba12c0ae3cd473ecfcc7791b53e64bf4904e48a8981b0f3dc73b866670266a26b9380052c')

_backports=(
    # current git 1.3.x branch
    "INKSCAPE_${pkgver//./_}..9f538bd40a737d7228cf47951e5566e1dbc953ac"

    # Fix build with poppler >= 24.05.0
    '96ca7a6c215aa14336b52753f56244dc8796ec43'
    # Fix build with >=poppler-24.03.0
    'f7e944575ea5247952d23f85dfa905cfa13f7b28'

    # Up C++ version to C++20.
    '1798e9c13b786f3d077ba0132592c4d5c1d1fb9b'
)

prepare() {
    cd "$_pkgname"

    git submodule set-url share/extensions ../inkscape-extensions/
    git submodule set-url src/3rdparty/2geom ../inkscape-lib2geom/
    # git submodule set-url share/themes ../inkscape-themes/
    git -c protocol.file.allow=always submodule update --init

    # (
    # cd share/extensions/
    # git submodule set-url other/gcodetools ../inkscape-extras-extensions-gcodetools/
    # git submodule set-url other/inkman ../inkscape-extras-extension-manager/
    # git submodule set-url other/clipart ../inkscape-extras-inkscape-import-clipart/
    # git submodule set-url other/extension-xaml ../inkscape-extras-extension-xaml/
    # git -c protocol.file.allow=always submodule update --init
    # )

    local _c
    for _c in "${_backports[@]}"; do
        git log --oneline -1 "${_c}"
        git cherry-pick -n "${_c}"
    done

    patch -Np1 < ../0001-Remove-device-manager.h-.cpp-and-src-ui-dialog-input.patch
}

build() {
    local cmake_options=(
        -B build
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX:PATH=/usr
        -W no-dev
        -S $_pkgname
    )

    cmake "${cmake_options[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --build build --target install
}
